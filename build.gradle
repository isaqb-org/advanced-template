import org.asciidoctor.gradle.jvm.AsciidoctorTask
import java.text.SimpleDateFormat


//
//dependencies {
//    classpath "org.asciidoctor:asciidoctorj-pdf:2.0.4"
//}

plugins {
    id "org.asciidoctor.jvm.convert" version "2.4.0"
    id "org.asciidoctor.jvm.pdf" version "2.4.0"
}
repositories {
    mavenCentral()
}

asciidoctorj {
    version = '1.6.2'
}

ext {
    today = new Date()
    versionDate = new SimpleDateFormat("yyyyMMdd").format(today)

    project.version = project.file("./document.version").text
    curriculumFileName = "curriculum-template"
    addSuffixToCurriculum = { suffix ->
        for (extension in ["html", "pdf"]) {
            File source = new File("${buildDir}/${curriculumFileName}.${extension}")
            File target = new File("${buildDir}/${curriculumFileName}${suffix}.${extension}")

            source.renameTo(target)
        }
    }
}




class RenderCurriculumTask extends AsciidoctorTask {
    @Inject
    RenderCurriculumTask(WorkerExecutor we, String curriculumFileName, String versionDate, String language, boolean withRemarks) {
        super(we)
        sourceDir = new File("./docs/")
        baseDir = new File ("./docs/")
        sources {
            include "index.adoc"
            include "${curriculumFileName}.adoc"
        }
        outputDir = new File("./build/")
        outputOptions {
            separateOutputDirs = false
            backends 'pdf', 'html5'
        }

        def fileVersion = project.version.trim() + "-" + language

        attributes = [
                'icons'             : 'font',
                'version-label'     : '',
                'revnumber'         : fileVersion,
                'revdate'           : versionDate,
                'document-version'  : fileVersion + "-" + versionDate,
                'currentDate'       : versionDate,
                'language'          : language,
                'withRemarks'       : withRemarks,
                'curriculumFileName': curriculumFileName,
                'debug_adoc'        : false,
                'pdf-stylesdir'     : '../pdf-theme/themes',
                'pdf-fontsdir'      : '../pdf-theme/fonts',
                'pdf-style'         : 'isaqb',
                'stylesheet'        : '../html-theme/adoc-github.css',
                'stylesheet-dir'    : '../html-theme'
        ]
    }
}

task buildDocs {
    group 'Documentation'
    description 'Grouping task for generating all languages in several formats'
    dependsOn "includeLearningObjectives", "renderNoRemarksDE", "renderNoRemarksEN"
}

task renderNoRemarksDE(type: RenderCurriculumTask,
        constructorArgs: [curriculumFileName, versionDate, "DE", false]) {
    doLast {
        addSuffixToCurriculum("-de")
    }
}

task renderWithRemarksDE(type: RenderCurriculumTask,
        constructorArgs: [curriculumFileName, versionDate, "DE", true]) {
    doLast {
        addSuffixToCurriculum("-remarks-de")
    }
}

task renderNoRemarksEN(type: RenderCurriculumTask,
        constructorArgs: [curriculumFileName, versionDate, "EN", false]) {
    doLast {
        addSuffixToCurriculum("-en")
    }
}

task renderWithRemarksEN(type: RenderCurriculumTask,
        constructorArgs: [curriculumFileName, versionDate, "EN", true]) {
    doLast {
        addSuffixToCurriculum("-remarks-en")
    }
}

apply from: 'scripts/includeLearningObjectives.gradle'

defaultTasks "buildDocs"
