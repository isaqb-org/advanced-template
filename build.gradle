import org.asciidoctor.gradle.AsciidoctorTask
import javax.inject.Inject
import java.text.SimpleDateFormat

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath "org.asciidoctor:asciidoctorj-pdf:1.5.3"
  }
}
plugins {
  id "org.asciidoctor.convert" version "1.6.0"
}

ext {
  today = new Date()
  englishDate = new SimpleDateFormat("MMMM d, yyyy", Locale.US).format(today)
  germanDate = new SimpleDateFormat("d. MMMM yyy", Locale.GERMANY).format(today)

  project.version = project.file("./document.version").text
  curriculumFileName = "curriculum-template"
  addSuffixToCurriculum = { suffix ->
    for (extension in ["html", "pdf"]) {
      File source = new File("${buildDir}/${curriculumFileName}.${extension}")
      File target = new File("${buildDir}/${curriculumFileName}${suffix}.${extension}")

      source.renameTo(target)
    }
  }
}

class RenderCurriculumTask extends AsciidoctorTask {
  @Inject
  RenderCurriculumTask(String curriculumFileName, String currentDate, String language, boolean withRemarks) {
    sourceDir = new File("./docs/")
    sources {
      include "index.adoc"
      include "${curriculumFileName}.adoc"
    }
    outputDir = new File("./build/")
    separateOutputDirs = false
    backends 'pdf', 'html5'

    def fileVersion = project.version.trim() + "-" + language

    attributes = [
      'icons'             : 'font',
      'revnumber'         : fileVersion,
      'revdate'           : currentDate,
      'document-version'  : fileVersion + " (" + currentDate + ")",
      'currentDate'       : currentDate,
      'language'          : language,
      'withRemarks'       : withRemarks,
      'curriculumFileName': curriculumFileName,
      'debug_adoc'        : false,
      'pdf-stylesdir'     : '../pdf-theme/themes',
      'pdf-fontsdir'      : '../pdf-theme/fonts',
      'pdf-style'         : 'isaqb'
    ]
  }
}

task buildDocs {
  group 'Documentation'
  description 'Grouping task for generating all languages in several formats'
  dependsOn "includeLearningObjectives", "renderNoRemarksDE", "renderNoRemarksEN", "renderWithRemarksDE", "renderWithRemarksEN"
}

task renderNoRemarksDE(type: RenderCurriculumTask,
    constructorArgs: [curriculumFileName, germanDate, "DE", false]) {
  doLast {
    addSuffixToCurriculum("-de")
  }
}

task renderWithRemarksDE(type: RenderCurriculumTask,
     constructorArgs: [curriculumFileName, germanDate, "DE", true]) {
  doLast {
    addSuffixToCurriculum("-remarks-de")
  }
}

task renderNoRemarksEN(type: RenderCurriculumTask,
    constructorArgs: [curriculumFileName, englishDate, "EN", false]) {
  doLast {
    addSuffixToCurriculum("-en")
  }
}

task renderWithRemarksEN(type: RenderCurriculumTask,
     constructorArgs: [curriculumFileName, englishDate, "EN", true]) {
  doLast {
    addSuffixToCurriculum("-remarks-en")
  }
}

apply from: 'scripts/includeLearningObjectives.gradle'

defaultTasks "buildDocs"
